_cfgdir=/opt/openresty/nginx/conf
_tmpdir=/var/lib/openresty
pkgname=openresty
pkgver=1.11.2.3
sslver=1.0.2k
pkgrel=1
pkgdesc="A Fast and Scalable Web Platform by Extending NGINX with Lua"
arch=('i686' 'x86_64')
url="http://openresty.org/"
license=('BSD')
depends=('perl>=5.6.1' 'readline' 'pcre' 'openssl')
install=$pkgname.install
source=(https://openresty.org/download/$pkgname-$pkgver.tar.gz
        https://www.openssl.org/source/openssl-$sslver.tar.gz
        service
        $pkgname.logrotate
        $pkgname.install)
noextract=()
md5sums=('f9ec79ecbb4a2c84f21d21ab743afac2'
         'f965fc0bf01bf882b31314b61391ae65'
         '0d2008647eb9cf8cff44d52d859d3030'
         'b38744739022876554a0444d92e6603b'
         '4b8c6fc00d048228757f8bade45a9211')
backup=(${_cfgdir:1}/fastcgi.conf
        ${_cfgdir:1}/fastcgi_params
        ${_cfgdir:1}/koi-win
        ${_cfgdir:1}/koi-utf
        ${_cfgdir:1}/mime.types
        ${_cfgdir:1}/nginx.conf
        ${_cfgdir:1}/scgi_params
        ${_cfgdir:1}/uwsgi_params
        ${_cfgdir:1}/win-utf
        etc/logrotate.d/openresty)

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/opt/openresty \
    --conf-path=$_cfgdir/nginx.conf \
    --user=http --group=http \
    --with-file-aio \
    --with-http_dav_module \
    --with-http_gzip_static_module \
    --with-http_realip_module \
    --with-http_ssl_module \
    --with-http_stub_status_module \
    --with-imap \
    --with-imap_ssl_module \
    --with-ipv6 \
    --with-luajit \
    --with-pcre-jit \
    --with-http_v2_module \
    --with-stream \
    --with-stream_ssl_module \
    --with-http_iconv_module \
    --with-openssl=../openssl-$sslver

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYRIGHT $pkgdir/usr/share/licenses/$pkgname/LICENSE
  install -d "$pkgdir"/etc/logrotate.d
  install -m644 "$srcdir"/openresty.logrotate "$pkgdir"/etc/logrotate.d/openresty
  install -d "$pkgdir"/$_tmpdir
  install -Dm644 "$srcdir/service" "$pkgdir/usr/lib/systemd/system/openresty.service"
  rm -rf "$pkgdir/var/run"
  if ! grep -i "/opt/openresty/bin/:/opt/openresty/nginx/sbin/" /etc/bash.bashrc > /dev/null
  	then echo -e "export PATH=\$PATH:/opt/openresty/bin/:/opt/openresty/nginx/sbin/ #Automatically added by openresty package\n" >> ~/.bashrc;
  fi
}
